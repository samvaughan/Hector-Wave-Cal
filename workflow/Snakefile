import pandas as pd
from pathlib import Path

df = pd.read_csv("resources/input_files.csv", comment="#", index_col="arc_id")
all_arc_IDs = df.index
all_frame_names = df.frame_name


def get_tlm_filename(wildcards):
    return df.loc[wildcards.arc_id, "tlm_filename"]


def get_dat_filename(wildcards):
    return df.loc[wildcards.arc_id, "dat_filename"]


def get_arc_filename(wildcards):
    return df.loc[wildcards.arc_id, "arc_filename"]


rule all:
    input:
        flag_files=expand("results/flags/{arc_id}_added.flag", arc_id=all_arc_IDs),
        fitted_parameters=expand(
            "results/FittedParameters/{arc_id}_parameters.nc",
            arc_id=all_arc_IDs,
        ),


rule make_database:
    output:
        database="results/database/arc_data.db",
    script:
        "scripts/create_db.py"


rule add_arc_frame_to_database:
    input:
        database=rules.make_database.output.database,
        dat_filename=get_dat_filename,
        tlm_filename=get_tlm_filename,
        arc_filename=get_arc_filename,
    output:
        flag_file="results/flags/{arc_id}_added.flag",
    script:
        "scripts/add_arc_frame_to_database.py"


rule fit_model_to_db_data:
    input:
        database=rules.make_database.output.database,
        flag_file=rules.add_arc_frame_to_database.output.flag_file,
    output:
        parameters="results/FittedParameters/{arc_id}_parameters.nc",
        plot_filename="results/Plots/{arc_id}_residuals.pdf",
    params:
        arc_id="{arc_id}",
        N_x=8,
        N_y=4,
        intensity_cut=100,
    script:
        "scripts/fit_arc_model_from_database.py"
